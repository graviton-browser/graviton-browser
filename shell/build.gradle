buildscript {
    ext.kotlin_version = '1.2.31'
    ext.slf4j_version = '1.7.25'
    ext.jna_version = '4.5.1'
    ext.tinylog_version = '1.3.4'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

group 'net.plan99.graviton'

apply plugin: 'java'
apply plugin: 'kotlin'

sourceCompatibility = 1.8

dependencies {
    // Kotlin - standard library for the Kotlin language
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.5"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-javafx:0.22.5"

    // Maven Resolver - a library for working with Maven repositories.
    if (true) {
        // We use a forked version that has upstream PR #10 merged in, which gives parallel dependency resolution.
        // This makes project fetch about twice as fast.
        compile 'com.github.mikehearn:maven-resolver:1e2ed26e5c6b7f439882339deae8ff4effec50ac'
    } else {
        // Used for local development.
        def maven_resolver_version = "1.1.2-SNAPSHOT"
        compile "org.apache.maven.resolver:maven-resolver-api:$maven_resolver_version"
        compile "org.apache.maven.resolver:maven-resolver-impl:$maven_resolver_version"
        compile "org.apache.maven.resolver:maven-resolver-connector-basic:$maven_resolver_version"
        compile "org.apache.maven.resolver:maven-resolver-transport-http:$maven_resolver_version"
        compile "org.apache.maven.resolver:maven-resolver-transport-file:$maven_resolver_version"
        compile "org.apache.maven.resolver:maven-resolver-util:$maven_resolver_version"
        compile "org.apache.maven.resolver:maven-resolver-spi:$maven_resolver_version"
    }
    compile "org.apache.maven:maven-resolver-provider:3.5.0"

    // Graviton Scheduler - an interface to operating system cron functionalities.
    compile project(":graviton-scheduler")

    // SLF4J and TinyLog - convenient, small and fast logging libraries.
    compile "org.slf4j:slf4j-api:$slf4j_version"
    compile "org.slf4j:jul-to-slf4j:$slf4j_version"
    compile "org.tinylog:tinylog:$tinylog_version"
    compile "org.tinylog:slf4j-binding:$tinylog_version"

    // ReactFX - reactive programming.
    compile "org.reactfx:reactfx:2.0-M5"

    // ProgressBar - ASCII art progress bar library.
    compile "me.tongfei:progressbar:0.6.0"

    // PicoCLI - command line parsing with pretty colours and useful help.
    compile 'info.picocli:picocli:3.0.0-alpha-3'

    // TornadoFX - a Kotlin UI framework based on JavaFX.
    compile "no.tornado:tornadofx:1.7.15"

    // JUnit - helpful unit testing framework.
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

    // JimFS - in memory file system useful for test mocks.
    testCompile "com.google.jimfs:jimfs:1.1"

    // JNA - easy native access without using JNI
    compile "net.java.dev.jna:jna:$jna_version"
    compile "net.java.dev.jna:jna-platform:$jna_version"

    // FastClasspathScanner - fast search engine over a set of JARs
    compile "io.github.lukehutch:fast-classpath-scanner:2.18.2"

    // YamlBeans - easy serialisation to and from the human-friendly Yaml text format.
    compile "com.esotericsoftware.yamlbeans:yamlbeans:1.13"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes(
            'Implementation-Version': "${project.parent.version}"
        )
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}