buildscript {
    ext.slf4j_version = '1.7.25'
    ext.jna_version = '4.5.2'
    ext.tinylog_version = '1.3.5'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.gradle:osdetector-gradle-plugin:1.6.0"
    }
}

group 'app.graviton'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'com.google.osdetector'

def isPostJ8 = JavaVersion.current().java9Compatible

if (isPostJ8) {
    println("Running on Java 9 or later, adjusting build")
    compileJava {
        options.compilerArgs << "--add-exports" << "javafx.graphics/com.sun.javafx.application=ALL-UNNAMED"
    }
}

dependencies {
    // Kotlin - standard library for the Kotlin language
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Maven Resolver - a library for working with Maven repositories.
    if (true) {
        // We use a forked version that has upstream PR #10 merged in, which gives parallel dependency resolution.
        // This makes project fetch about twice as fast.
        compile('com.github.mikehearn:maven-resolver:paralleldeps') {
            exclude group: "com.github.mikehearn.maven-resolver", module: "maven-resolver-demo-snippets"
            exclude group: "com.github.mikehearn.maven-resolver", module: "maven-resolver-demo-maven-plugin"
            exclude group: "com.github.mikehearn.maven-resolver", module: "maven-resolver-test-util"
            exclude group: "com.github.mikehearn.maven-resolver", module: "maven-resolver-transport-wagon"
        }
    } else {
        // Used for local development.
        def maven_resolver_version = "1.1.2-SNAPSHOT"
        compile "org.apache.maven.resolver:maven-resolver-api:$maven_resolver_version"
        compile "org.apache.maven.resolver:maven-resolver-impl:$maven_resolver_version"
        compile "org.apache.maven.resolver:maven-resolver-connector-basic:$maven_resolver_version"
        compile "org.apache.maven.resolver:maven-resolver-transport-http:$maven_resolver_version"
        compile "org.apache.maven.resolver:maven-resolver-transport-file:$maven_resolver_version"
        compile "org.apache.maven.resolver:maven-resolver-util:$maven_resolver_version"
        compile "org.apache.maven.resolver:maven-resolver-spi:$maven_resolver_version"
    }
    compile("org.apache.maven:maven-resolver-provider:3.5.4") {
        exclude group: "org.apache.maven.resolver", module: "*"
    }

    // Graviton Scheduler - an interface to operating system cron functionalities.
    compile project(":graviton-scheduler")
    
    // Graviton API - a way for apps to talk to the shell host.
    compile project(":api")

    // SLF4J and TinyLog - convenient, small and fast logging libraries.
    compile "org.slf4j:slf4j-api:$slf4j_version"
    compile "org.slf4j:jul-to-slf4j:$slf4j_version"
    compile "org.tinylog:tinylog:$tinylog_version"
    compile "org.tinylog:slf4j-binding:$tinylog_version"

    // ProgressBar - ASCII art progress bar library.
    compile "me.tongfei:progressbar:0.7.1"

    // PicoCLI - command line parsing with pretty colours and useful help.
    compile 'info.picocli:picocli:3.6.1'

    // TornadoFX - a Kotlin UI framework based on JavaFX.
    compile "no.tornado:tornadofx:1.7.17"

    // JUnit - helpful unit testing framework.
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

    // JimFS - in memory file system useful for test mocks.
    testCompile "com.google.jimfs:jimfs:1.1"

    // JNA - easy native access without using JNI
    compile "net.java.dev.jna:jna:$jna_version"
    compile "net.java.dev.jna:jna-platform:$jna_version"

    // FastClasspathScanner - fast search engine over a set of JARs
    // TODO: Port to ClassGraph
    compile "io.github.lukehutch:fast-classpath-scanner:2.18.2"

    // YamlBeans - easy serialisation to and from the human-friendly Yaml text format.
    compile "com.esotericsoftware.yamlbeans:yamlbeans:1.13"

    // Conscrypt - BoringSSL access from Java
    compile 'org.conscrypt:conscrypt-openjdk:1.3.0:' + osdetector.classifier

    // OkHttp - good HTTP client library
    // TODO - Write a Maven Resolver backend that uses OkHttp so we can lose Apache HttpClient
    compile 'com.squareup.okhttp3:okhttp:3.11.0'

    // NSMenuFX - library for controlling the Mac native menu bar.
    compile 'de.codecentric.centerdevice:centerdevice-nsmenufx:2.1.6'

    // Proxy Vole - auto-detection of HTTP proxy settings from the operating system and execution of PAC files.
    compile 'com.github.markusbernhardt:proxy-vole:1.0.5'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

if (isPostJ8) {
    sourceCompatibility = "10"
    targetCompatibility = "10"
}

jar {
    manifest {
        attributes(
                'Implementation-Version': "${project.parent.version}"
        )
    }
}