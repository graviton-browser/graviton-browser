import org.apache.tools.ant.taskdefs.condition.Os

group 'net.plan99.graviton'

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://dl.bintray.com/jetbrains/kotlin-native-dependencies"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-native-gradle-plugin:$kotlin_native_version"
    }
}

apply plugin: 'konan'

konanArtifacts {
    program('bootstrap') {
        target('macbook') {
            srcFiles 'src/main/kotlin/mac.kt', 'src/main/kotlin/bootstrap-posix.kt'
        }
        target('mingw') {
            srcFiles 'src/main/kotlin/win.kt'
            linkerOpts "-Wl,--subsystem,windows"
        }
        target('linux') {
            srcFiles 'src/main/kotlin/linux.kt', 'src/main/kotlin/bootstrap-posix.kt'
        }
    }

    // Windows programs have to be tagged in the EXE headers as either 'console' or 'gui' and there's no way to win,
    // both modes suck in different ways. So we have to produce two binaries here.
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        program('bootstrap-console') {
            target('mingw') {
                srcFiles 'src/main/kotlin/win.kt'
                linkerOpts "-Wl,--subsystem,console"
            }
        }
    }
}